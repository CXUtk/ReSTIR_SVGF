#pragma once
#pragma max_recursion_depth 1
#include "../Library/Utils.hlsl"
#include "SVGFStructure.hlsl"
#include "RayTraceLighting.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Sampling/Sampling.hlsl"

RWTexture2D<float4> _renderTarget;
RWStructuredBuffer<restir_RESERVOIR> _curRestirBuffer;
StructuredBuffer<restir_RESERVOIR> _prevRestirBuffer;


Texture2D<float> _gdepth;
Texture2D<float4> _albedoR;
Texture2D<float4> _normalM;
Texture2D<float2> _motionVector;
Texture2D<float4> _worldPos;

Texture2D<float> _gdepth_prev;
Texture2D<float4> _albedoR_prev;
Texture2D<float4> _normalM_prev;
Texture2D<float2> _motionVector_prev;
Texture2D<float4> _worldPos_prev;


TextureCube _CubeMap;

SamplerState my_point_clamp_sampler;

uint _screenWidth;
uint _screenHeight;
uint _uGlobalFrames;

uint _PathBounces;

[shader("anyhit")] // Add to hit group #0
void ShadowAnyHit(inout PathTracingPayload pay, BuiltInTriangleIntersectionAttributes attrib) 
{
    pay.L = 0;
}

[shader("miss")]
void MyMissShader(inout PathTracingPayload payload : SV_RayPayload)
{
    float3 Le = _CubeMap.SampleLevel(my_point_clamp_sampler, WorldRayDirection(), 0).xyz * 3;
    payload.L = 1;
    payload.E = Le;
    payload.N = -WorldRayDirection();
}


void DoPathTracing(int2 dispatchIdx, inout uint seed, out float3 LDirect, out float3 LIndirect, 
out float3 hitN, out float3 hitPos, out float Pdf, out float3 brdfNoLDivPdfindirect)
{
    float a = wang_hash(seed) / 4294967295.0;
    float b = wang_hash(seed) / 4294967295.0;
    
    float4 normalM = _normalM[dispatchIdx];
    float3 worldPos = _worldPos[dispatchIdx].xyz;
    float3 wo = normalize(_WorldSpaceCameraPos.xyz - worldPos);
    if(normalM.x == 0 && normalM.y == 0 && normalM.z == 0)
    {
        return;
    }
    float3 N = normalize(normalM.xyz * 2 - 1);

    float4 albedoR = _albedoR[dispatchIdx];
    
    Surface surface;
    surface.worldPos = worldPos.xyz;
    surface.normal = N;
    surface.color = albedoR.xyz;
    surface.alpha = 1;
    surface.roughness = albedoR.a;
    surface.metallic = normalM.a;
    surface.emission = 0;

    float3 wi;
    float pdf;
    float3 brdfNoLDivPdf = GGXImportanceSample_NoAlbedo(float2(a, b), surface, wo, wi, pdf);
    Pdf = pdf;
    brdfNoLDivPdfindirect = brdfNoLDivPdf;
    

    float3 beta = 1;
    LDirect = (Direct_DirectionalLight_alter(surface, wo, 1) + Direct_AreaLight_alter(surface, wo, seed, 1));
    LIndirect = 0;

    seed = seed + 771132;

    for(int i = 0; i < _PathBounces; i++)
    {
        RayDesc ray;
        ray.Origin = worldPos + N * 1e-3; 
        ray.Direction = wi;
        ray.TMin = 0;
        ray.TMax = 999999;

        PathTracingPayload payload = CreatePathTracingPayload();
        TraceRay(_RaytracingAccelerationStructure, RAY_FLAG_NONE, 0xFFFFFFF, 0, 1, 0, ray, payload);

        Surface surfaceHit;
        surfaceHit.worldPos = ray.Origin + payload.T * ray.Direction;
        surfaceHit.normal = payload.N;
        surfaceHit.color = payload.Albedo;
        surfaceHit.alpha = 1;
        surfaceHit.roughness = payload.Roughness;
        surfaceHit.metallic = payload.Metallic;
        surfaceHit.emission = payload.E;

        // // Add emission
        // LIndirect += beta * surfaceHit.emission;
        
        if (i == 0)
        {
            hitN = surfaceHit.normal;
            hitPos = surfaceHit.worldPos;
        }

        if (payload.T == 999999)
        {
            LIndirect += beta * surfaceHit.emission;
            break;
        }

        wo = -wi;
        LIndirect += beta * (Direct_DirectionalLight_alter(surfaceHit, wo, surfaceHit.color) + Direct_AreaLight_alter(surfaceHit, wo, seed, surfaceHit.color));

        seed = seed + 2334532;

        float x = wang_hash(seed) / 4294967295.0;
        float y = wang_hash(seed) / 4294967295.0;
        beta *= GGXImportanceSample(float2(x, y), surfaceHit, wo, wi, pdf);

        worldPos = surfaceHit.worldPos;
        N = surfaceHit.normal;
    }
}

[shader("raygeneration")]
void MyRaygenShader()
{
    uint2 dispatchIdx = DispatchRaysIndex().xy;
    
    uint seed = _uGlobalFrames * 2242231u + dispatchIdx.y * 1923u + dispatchIdx.x;

    float3 LDirect, LIndirect;
    float3 hitN, hitPos, brdfNoLDivPdfindirect;
    float pdf;
    DoPathTracing(dispatchIdx, seed, LDirect, LIndirect, hitN, hitPos, pdf, brdfNoLDivPdfindirect);

    float4 newColor = float4(LDirect + LIndirect * brdfNoLDivPdfindirect, 1);
    
    if (isnan(newColor.x) || isnan(newColor.y) || isnan(newColor.z))
    {
        _renderTarget[dispatchIdx] = float4(1, 1, 0, 1);
        return;
    }
    _renderTarget[dispatchIdx] = float4(LDirect + LIndirect * brdfNoLDivPdfindirect, 1);// * float4(surface.color, 1);
}


RWTexture2D<float4> _directTarget;
RWTexture2D<float4> _indirectTarget;

[shader("raygeneration")]
void SeperatePathTracing()
{
    uint2 dispatchIdx = DispatchRaysIndex().xy;
    
    uint seed = _uGlobalFrames * 2242231u + dispatchIdx.y * 1923u + dispatchIdx.x;

    float3 LDirect, LIndirect;
    float3 hitN, hitPos, brdfNoLDivPdfindirect;
    float pdf;
    DoPathTracing(dispatchIdx, seed, LDirect, LIndirect, hitN, hitPos, pdf, brdfNoLDivPdfindirect);
    
    if (isnan(LDirect.x) || isnan(LDirect.y) || isnan(LDirect.z))
    {
        _directTarget[dispatchIdx] = float4(1, 0, 1, 1);
        return;
    }

    if (isnan(LIndirect.x) || isnan(LIndirect.y) || isnan(LIndirect.z))
    {
        _indirectTarget[dispatchIdx] = float4(1, 0, 1, 1);
        return;
    }
    _directTarget[dispatchIdx] = float4(LDirect, 1);// * float4(surface.color, 1);
    _indirectTarget[dispatchIdx] = float4(LIndirect * brdfNoLDivPdfindirect, 1);
}

bool temporal_reject(int2 dispatchIdx, out int2 prevCoord)
{
    bool reject = false;
    float2 uv = (dispatchIdx + 0.5) / float2(_screenWidth, _screenHeight);

    float2 motion = _motionVector[dispatchIdx];
    float2 pixel_prev = (uv + motion) * float2(_screenWidth, _screenHeight);
    int2 imageCoord_prev = floor(pixel_prev);
    prevCoord = imageCoord_prev;

    if (imageCoord_prev.x < 0 || imageCoord_prev.x >= _screenWidth 
    || imageCoord_prev.y < 0 || imageCoord_prev.y >= _screenHeight)
    {
        return true;
    }
    
    float4 cur_WSpos = _worldPos[dispatchIdx];
    float3 cur_normalN = _normalM[dispatchIdx];
    // Current is empty pixel
    if(length(cur_normalN) < 1e-5)
    {
        return true;
    }
    float3 cur_N = normalize(cur_normalN * 2 - 1);

    float3 prev_normalN = _normalM_prev[imageCoord_prev];
    if(length(prev_normalN) < 1e-5)
    {
        return true;
    }
    float3 prev_normal = normalize(prev_normalN * 2 - 1);
    float4 prev_WSPos = _worldPos_prev[imageCoord_prev];
    float3 dir = prev_WSPos.xyz - cur_WSpos.xyz;
    if (cur_WSpos.w != prev_WSPos.w || dot(prev_normal, cur_N) < 0.7 || abs(dot(cur_N, dir)) > 0.1)
    {
        return true;
    }
    return false;
}

[shader("raygeneration")]
void SeperatePathTracing_ReSTIR()
{
    uint2 dispatchIdx = DispatchRaysIndex().xy;

    float4 normalM = _normalM[dispatchIdx];
    float3 worldPos = _worldPos[dispatchIdx].xyz;
    float3 wo = normalize(_WorldSpaceCameraPos.xyz - worldPos);
    if(length(normalM.xyz) < 1e-5)
    {
        return;
    }
    float3 N = normalize(normalM.xyz * 2 - 1);
    float4 albedoR = _albedoR[dispatchIdx];
    
    Surface surface;
    surface.worldPos = worldPos.xyz;
    surface.normal = N;
    surface.color = albedoR.xyz;
    surface.alpha = 1;
    surface.roughness = albedoR.a;
    surface.metallic = normalM.a;
    surface.emission = 0;
    
    
    uint seed = _uGlobalFrames * 2242231u + dispatchIdx.y * 1923u + dispatchIdx.x;

    float3 LDirect, LIndirect;
    float3 hitN, hitPos, brdfNoLDivPdfindirect;
    float pdf;
    DoPathTracing(dispatchIdx, seed, LDirect, LIndirect, hitN, hitPos, pdf, brdfNoLDivPdfindirect);

    
    // ------------------- ReSTIR process
    float3 Lo = LIndirect;

    restir_RESERVOIR R;
    R.sample.Xv = worldPos + N * 1e-3;
    R.sample.Nv = N;
    R.sample.Xs = hitPos;
    R.sample.Ns = hitN; 
    R.sample.Lo = Lo;


    float pQ = Luminance(Lo) / pdf;
    R.w = pQ;
    R.M = 1;
    R.Wout = 1 / pdf;


    int bufferId = dispatchIdx.y * _screenWidth + dispatchIdx.x;
    int2 prevCoord;
    
    float3 oldColor = LIndirect * brdfNoLDivPdfindirect;
    float3 newColor = oldColor;
    
    if(temporal_reject(dispatchIdx, prevCoord))
    {
        if (Luminance(Lo) == 0)
        {
            R.w = 0;
            R.M = 0;
            R.Wout = 0;
        }
        _curRestirBuffer[bufferId] = R;
        float3 WI = normalize(R.sample.Xs - R.sample.Xv);
        newColor = BRDFNoL_GGX_NoAlbedo(surface, WI, wo) * R.sample.Lo * R.Wout;
        // newColor = Lo * BRDFNoL_GGX_NoAlbedo(surface, WI, wo) / pdf;
    }
    else
    {
        uint seed2 = _uGlobalFrames * 7714523u + dispatchIdx.y * 7732u + dispatchIdx.x * 3;
        float p = wang_hash(seed2) / 4294967295.0;

        uint bufferId2 = prevCoord.y * _screenWidth + prevCoord.x;
        restir_RESERVOIR old_sample = _prevRestirBuffer[bufferId2];

        float w = R.w;
        RESERVOIR_update(old_sample, R.sample, w, p);
        old_sample.Wout = old_sample.w / (old_sample.M * Luminance(old_sample.sample.Lo));
        if(Luminance(old_sample.sample.Lo) == 0)
        {
            old_sample.Wout = 0;
        }
        _curRestirBuffer[bufferId] = old_sample;
        float3 WI = normalize(old_sample.sample.Xs - old_sample.sample.Xv);
        newColor = BRDFNoL_GGX_NoAlbedo(surface, WI, wo) * old_sample.sample.Lo * old_sample.Wout;
    }
    
    // ------------------- ReSTIR process END

    if (isnan(LDirect.x) || isnan(LDirect.y) || isnan(LDirect.z))
    {
        _directTarget[dispatchIdx] = float4(1, 0, 1, 1);
    }
    else
    {
        _directTarget[dispatchIdx] = float4(LDirect, 1);
    }

    if (isnan(oldColor.x) || isnan(oldColor.y) || isnan(oldColor.z))
    {
        _indirectTarget[dispatchIdx] = float4(1, 0, 1, 1);
    }
    else
    {
        _indirectTarget[dispatchIdx] = float4(oldColor, 1);
    }
}


[shader("raygeneration")]
void ReSTIR_SpatialReuse()
{
    int2 imageCoord = DispatchRaysIndex().xy;
    int bufferId = imageCoord.y * _screenWidth + imageCoord.x;
    uint seed = _uGlobalFrames * 731452u + bufferId;

    float4 cur_WSpos = _worldPos[imageCoord];
    float3 cur_normalN = _normalM[imageCoord];
    // Current is empty pixel
    if(length(cur_normalN) < 1e-5)
    {
        return;
    }
    
    float3 cur_N = normalize(cur_normalN * 2 - 1);

    int2 prevCoord;
    
    restir_RESERVOIR Q = _prevRestirBuffer[bufferId];

    const int Radius = 10;
    for(int i = 0; i < 5; i++)
    {
        float x = wang_hash(seed) / 4294967295.0;
        float y = wang_hash(seed) / 4294967295.0;
        
        int dx = (x * 2 - 1) * Radius;
        int dy = (y * 2 - 1) * Radius;
        
        int2 coord = imageCoord + int2(dx, dy);
        
        if (coord.x < 0 || coord.x >= _screenWidth || coord.y < 0 || coord.y >= _screenHeight)
        {
            continue;
        }
        
        if(coord.x == imageCoord.x && coord.y == imageCoord.y)
        {
            continue;
        }
        
        float3 q_normalN = _normalM[coord];
        if(length(q_normalN) < 1e-5)
        {
            continue;
        }
        float3 q_normal = normalize(q_normalN * 2 - 1);
        float4 q_WSPos = _worldPos[coord];
        float3 dir = q_WSPos.xyz - cur_WSpos.xyz;
        if (length(cur_WSpos.xyz - q_WSPos.xyz) > 3 || dot(q_normal, cur_N) < 0.9 || abs(dot(cur_N, dir)) > 0.1)
        {
            continue;
        }
        
        int bufferId2 = coord.y * _screenWidth + coord.x;
        restir_RESERVOIR Rn = _prevRestirBuffer[bufferId2];

        PathTracingPayload shadowPayLoad = CreatePathTracingPayload();
        
        RayDesc shadowRay;
        float3 origin = cur_WSpos.xyz + cur_N * 1e-3;
        shadowRay.Origin = origin; 
        shadowRay.Direction = Rn.sample.Xs - origin;
        shadowRay.TMin = 0;
        shadowRay.TMax = -1;

        TraceRay(_RaytracingAccelerationStructure, (RAY_FLAG_SKIP_CLOSEST_HIT_SHADER | RAY_FLAG_ACCEPT_FIRST_HIT_AND_END_SEARCH), 
        0xFF, 0, 1, 0, shadowRay, shadowPayLoad);
        
        float3 Qdir = Rn.sample.Xs - cur_WSpos.xyz;
        float3 Rdir = Rn.sample.Xs - Rn.sample.Xv;
        
        float a = abs(dot(Rn.sample.Ns, normalize(Qdir))) / abs(dot(Rn.sample.Ns, normalize(Rdir)));
        float b = dot(Rdir, Rdir) / dot(Qdir, Qdir);
        float J = a * b;
        
        float Pz = Luminance(Rn.sample.Lo * shadowPayLoad.L);
        Rn.sample.Xv = cur_WSpos.xyz;
        RESERVOIR_merge_spataial(Q, Rn, Pz, wang_hash(seed) / 4294967295.0);
    }

    if(Luminance(Q.sample.Lo) > 0)
    {
        Q.Wout = Q.w / (Q.M * Luminance(Q.sample.Lo));
    }
    else
    {
        Q.Wout = 0;
    }
    
    _curRestirBuffer[bufferId] = Q;
}